plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}


def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "net.defined.mobile_nebula"

    compileSdkVersion 34

    // default ndk version for AGP 8.7: https://developer.android.com/build/releases/past-releases/agp-8-7-0-release-notes
    ndkVersion "27.0.12077973"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "net.defined.mobile_nebula"
        minSdkVersion 26 //flutter.minSdkVersion
        targetSdkVersion 34 //flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(new FileInputStream(file('../key.properties')))
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
            storeFile file(properties['storeFile'])  // 绝对路径，不会错
            storePassword properties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'app_name', '"Nebula"'
        }

        debug {
            resValue 'string', 'app_name', '"Nebula-DEBUG"'
            applicationIdSuffix '.debug'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

flutter {
    source '../..'
}

dependencies {
    def workVersion = "2.9.1"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "com.google.guava:guava:31.0.1-android"
    implementation project(':mobileNebula')

}

